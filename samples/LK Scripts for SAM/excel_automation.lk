// Sample LK script demonstrating how to read and write data from an Excel file

conf= configuration();
tech = conf[0];
fin = conf[1];
if ( tech != "Flat Plate PV" ) {
	outln( "Script will only run with Detailed Photovoltaic performance model. Exiting.");
	exit;
}	
if ( fin == "none" ) {
	outln( "Script requires a financial model. Exiting");
	exit;
}
outln("Case configuration OK.");	

c_module = get("per_module");
outln( "Module cost from SAM System Costs page is " + c_module + "." );

u = get("module_costunits");
u_str = "$";
if ( u == 0 ) u_str = "$/Wdc";
outln( "Module cost units are " + u_str + "." );

xl_file = choose_file( homedir() , "Choose Excel File" , "Excel Files (*.xlsx)|*.xlsx" , 0 , 0 );
outln( "Excel file to open is " + xl_file + ".");

xl_obj = xl_create( );
xl_open( xl_obj , xl_file , 0 );
outln( "Excel file is open." );

xl_set( xl_obj , c_module , "module" );
outln ( "Module cost in Excel is set to " + c_module + " $/Wdc." );

c_inverter_str = xl_get( xl_obj, "inverter" );
c_inverter = to_real( c_inverter_str );
outln( "Inverter cost from Excel file is " + c_inverter + " $/Wdc." );

xl_free( xl_obj);
outln( "Excel file is closed." );

err = set( "inverter_costunits" , u+1 );
if ( err == false ) outln( "Inverter cost units on System Costs page are set to " + u_str + "." );
else outln( "Error: " + err );

err = set( "per_inverter" , c_inverter );
if ( err == false ) outln( "Inverter cost on System Costs page is set to " + c_inverter + " $/Wdc." );
else outln( "Error: " + err );

outln( "All done." );

